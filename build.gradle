buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		google()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		jcenter()
	}
	dependencies {
		classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects) {
	
	apply plugin: 'java-library'
	apply plugin: 'groovy'
	
	dependencies {
		implementation "org.codehaus.groovy:groovy:2.+"
	}
	
	sourceCompatibility = 1.8
	compileJava {
		options.incremental = true
	}
	compileGroovy {
		options.incremental = true
	}
	
	[compileJava, compileTestJava, compileGroovy, compileTestGroovy]*.options*.encoding = 'UTF-8'
	
	//
	// Here, we force the Java plugin to compile nothing,
	// and the Groovy plugin to compile everything.
	sourceSets {
		main {
			java {
				srcDirs = []
			}
			groovy { srcDirs = [ "src/main/java" ] }
			
			java.outputDir = groovy.outputDir
		}
	}
}

subprojects {
	version = '0.0.1-SNAPSHOT'
	ext.appName = 'hivemind'
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
		jcenter()
	}
}

// Clearing Eclipse project data in root folder:
tasks.eclipse.doLast {
	delete '.project'
	delete '.classpath'
	delete '.settings/'
}
